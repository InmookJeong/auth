plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.4'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'org.asciidoctor.jvm.convert' version '3.3.2'
}

group = 'kr.mook'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
	asciidoctorExt // spring rest docs
}

repositories {
	mavenCentral()
}

ext {
	set('snippetsDir', file("build/generated-snippets"))
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	compileOnly 'org.projectlombok:lombok:1.18.38'
    annotationProcessor 'org.projectlombok:lombok:1.18.38'
	implementation 'org.springframework.boot:spring-boot-starter-jdbc'	// JDBC Dependency
	runtimeOnly 'com.mysql:mysql-connector-j'	// MySQL Connector
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
	asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor' // spring rest docs
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	outputs.dir snippetsDir
	useJUnitPlatform()
}

tasks.named('asciidoctor') {
	inputs.dir snippetsDir
	dependsOn test
	configurations 'asciidoctorExt'	// spring reest docs
}

// spring rest docs
tasks.register('copyDocument', Copy) {
	dependsOn asciidoctor
	from layout.buildDirectory.dir("docs/asciidoc")
	into layout.projectDirectory.dir("src/main/resources/static/docs")
}

// spring rest docs
tasks.named('build') {
	dependsOn copyDocument
}